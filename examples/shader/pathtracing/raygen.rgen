#version 460
#extension GL_EXT_ray_tracing : enable
#include "globals.glsl"
#include "random.glsl"

layout(set = 0, binding = 0, rgba8) uniform image2D image;
layout(set = 0, binding = 1) uniform accelerationStructureEXT topLevelAS;
layout(set = 0, binding = 4) uniform UniformBuffer
{
    mat4 invView;
    mat4 invProj;
    mat4 transform;
    vec3 lightDirection;
} uniformBuffer;

layout(location = 0) rayPayloadEXT HitPayload payLoad;

void traceRay(vec4 origin, vec4 direction)
{
    traceRayEXT(
        topLevelAS,
        gl_RayFlagsOpaqueEXT,
        0xff, // cullMask
        0,    // sbtRecordOffset
        0,    // sbtRecordStride
        0,    // missIndex
        origin.xyz,
        0.01,
        direction.xyz,
        1000.0,
        0     // payloadLocation
    );
}

vec3 tonemap(in vec3 color, in float white)
{
  return clamp(color * (1 + color / white) / (1 + color), 0, 1);
}

vec3 gammaCorrect(in vec3 color)
{
    return pow(color, vec3(1.0/2.2));
}

void main()
{
    uint maxDepth = 8;
    uint maxSamples = 64;

    vec3 color = vec3(0.0);
    for(uint sampleCount = 0; sampleCount < maxSamples; sampleCount++){
        // Calc seed
        payLoad.seed = initRandom(gl_LaunchSizeEXT.xy, gl_LaunchIDEXT.xy, sampleCount);

        // Calc ray
        const vec2 screenPos = vec2(gl_LaunchIDEXT.xy) + vec2(rand(payLoad.seed), rand(payLoad.seed));
        const vec2 inUV = screenPos / vec2(gl_LaunchSizeEXT.xy);
        vec2 d = inUV * 2.0 - 1.0;

        vec4 origin    = uniformBuffer.invView * vec4(0, 0, 0, 1);
        vec4 target    = uniformBuffer.invProj * vec4(d.x, d.y, 0, 1) ;
        vec4 direction = uniformBuffer.invView * vec4(normalize(target.xyz), 0) ;

        payLoad.hit = false;
        payLoad.color = vec3(0.0);
        payLoad.weight = vec3(1.0);
        for(int depth = 0; depth < maxDepth; depth++){
            traceRay(origin, direction);
            if(!payLoad.hit){
                break;
            }

            origin.xyz = payLoad.pos;
            direction.xyz = payLoad.nextDir;
        }
        color += payLoad.color;
    }

    color /= float(maxSamples);
    imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(gammaCorrect(tonemap(color, 1000000000)), 1.0));
}
